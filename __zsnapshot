# Used variables: __zsnap_k __zsnap_t __zsnap_opts __zsnap_tmp

#
# "Serialize" parameters
#

# k, t, opts, tmp, tmp2
unset      __zsnap_k __zsnap_t __zsnap_opts __zsnap_tmp __zsnap_tmp2
typeset -g __zsnap_k __zsnap_t __zsnap_opts __zsnap_tmp __zsnap_tmp2

echo "#"
echo "# Variables"
echo "#"

cat <<EOF
for __zsnap_k in "\${(k)parameters[@]}"; do
    __zsnap_t="\${parameters[\$__zsnap_k]}"
    if [[ "\$__zsnap_t" != *-readonly* ]]; then
        unset "\$__zsnap_k"
    fi
done
EOF

echo

for __zsnap_k in "${(k)parameters[@]}"; do
    __zsnap_t="${parameters[$__zsnap_k]}"

    [[ "$__zsnap_k" = "__zsnap_k" || "$__zsnap_k" = "__zsnap_t" || "$__zsnap_k" = "__zsnap_opts" ]] && continue
    [[ "$__zsnap_k" = "__zsnap_tmp" || "$__zsnap_k" = "__zsnap_tmp2" ]] && continue
    [[ "$__zsnap_k" = "0" ]] && continue

    __zsnap_opts="-"
    if [[ "$__zsnap_t" = *-export* ]]; then
        __zsnap_opts+="x"
    fi
    if [[ "$__zsnap_t" = *-hideval* ]]; then
        __zsnap_opts+="H"
    fi
    if [[ "$__zsnap_t" = *-hide-* || "$__zsnap_t" = *-hide ]]; then
        __zsnap_opts+="h"
    fi
    if [[ "$__zsnap_t" = *-readonly* ]]; then
        __zsnap_opts+="r"
    fi
    if [[ "$__zsnap_t" = *-tag* ]]; then
        __zsnap_opts+="__zsnap_t"
    fi
    if [[ "$__zsnap_t" = *-unique* ]]; then
        __zsnap_opts+="U"
    fi
    if [[ "$__zsnap_t" = *-lower* ]]; then
        __zsnap_opts+="l"
    fi
    if [[ "$__zsnap_t" = *-upper* ]]; then
        __zsnap_opts+="u"
    fi
    if [[ "$__zsnap_t" = *-left* ]]; then
        # Check for field width
        __zsnap_tmp=`declare -p ${(q)__zsnap_k}`
        __zsnap_tmp="${__zsnap_tmp##*-L}"
        __zsnap_tmp="${__zsnap_tmp%% *}"
        __zsnap_opts="-L$__zsnap_tmp $__zsnap_opts"
    fi
    if [[ "$__zsnap_t" = *-right_blanks* ]]; then
        # Check for field width
        __zsnap_tmp=`declare -p ${(q)__zsnap_k}`
        __zsnap_tmp="${__zsnap_tmp##*-R}"
        __zsnap_tmp="${__zsnap_tmp%% *}"
        __zsnap_opts="-R$__zsnap_tmp $__zsnap_opts"
    fi
    if [[ "$__zsnap_t" = *-right_zeros* ]]; then
        # Check for field width
        __zsnap_tmp=`declare -p ${(q)__zsnap_k}`
        __zsnap_tmp="${__zsnap_tmp##*-Z}"
        __zsnap_tmp="${__zsnap_tmp%% *}"
        __zsnap_opts="-Z$__zsnap_tmp $__zsnap_opts"
    fi
    if [[ "$__zsnap_t" = *-special* ]]; then
        if [[ "$__zsnap_t" = *-hide-* || "$__zsnap_t" = *-hide || "$__zsnap_t" = *-readonly* ]]; then
            # Skip variables that are hidden or readonly, and special
            continue
        fi
    fi

    __zsnap_opts="${__zsnap_opts%-}"

    if [[ "$__zsnap_t" = "array" || "$__zsnap_t" = array-* ]]; then
        __zsnap_opts="-a $__zsnap_opts"
        echo "declare $__zsnap_opts $__zsnap_k"
        echo "$__zsnap_k=("
        eval "__zsnap_tmp=\${#$__zsnap_k}"
        for (( __zsnap_tmp2=1; __zsnap_tmp2 <= __zsnap_tmp; __zsnap_tmp2 ++ )); do
            eval "echo -E \"\${(qq)${__zsnap_k}[__zsnap_tmp2]}\""
        done
        echo ")"
    elif [[ "$__zsnap_t" = "association" || "$__zsnap_t" = association-* ]]; then
        __zsnap_opts="-A $__zsnap_opts"
        echo "declare $__zsnap_opts $__zsnap_k"
        eval "echo \"$__zsnap_k=( \${(qqkv)${__zsnap_k}[@]} )\""
    elif [[ "$__zsnap_t" = "scalar" || "$__zsnap_t" = scalar-* ]]; then
        echo "declare $__zsnap_opts $__zsnap_k"
        eval "echo \"$__zsnap_k=\${(qq)$__zsnap_k}\""
    elif [[ "$__zsnap_t" = "integer" || "$__zsnap_t" = integer-* ]]; then
        # Check for base
        __zsnap_tmp=`declare -p ${(q)__zsnap_k}`
        __zsnap_tmp="${__zsnap_tmp##*-i}"
        __zsnap_tmp="${__zsnap_tmp%% *}"
        __zsnap_opts="-i$__zsnap_tmp $__zsnap_opts"
        echo "declare $__zsnap_opts $__zsnap_k"
        eval "echo \"$__zsnap_k=\${$__zsnap_k}\""
    elif [[ "$__zsnap_t" = "float" || "$__zsnap_t" = float-* ]]; then
        __zsnap_tmp=`declare -p ${(q)__zsnap_k}`
        # -E or -F, get the base
        if [ "$__zsnap_tmp" = "${__zsnap_tmp##*-F}" ]; then
            __zsnap_tmp="${__zsnap_tmp##*-E}"
            __zsnap_tmp="${__zsnap_tmp%% *}"
            __zsnap_opts="-E$__zsnap_tmp $__zsnap_opts"
        else
            __zsnap_tmp="${__zsnap_tmp##*-F}"
            __zsnap_tmp="${__zsnap_tmp%% *}"
            __zsnap_opts="-F$__zsnap_tmp $__zsnap_opts"
        fi
        eval "echo \"$__zsnap_k=\${$__zsnap_k}\""
    fi

done

#
# "Serialize" functions
#

echo
echo "#"
echo "# Functions"
echo "#"
echo "unfunction -m \*"
declare -f

#
# Serialize zstyles
#

echo
echo "#"
echo "# Zstyles"
echo "#"
echo "zstyle -d"
zstyle -L

# vim:ft=zsh
